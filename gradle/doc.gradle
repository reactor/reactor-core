/*
 * Copyright (c) 2011-2017 Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
configure(rootProject) {
	apply plugin: 'org.asciidoctor.convert'

  configure(subprojects) { p ->
	if (p.name == 'benchmarks')
	  return
	
	p.tasks.withType(Javadoc).all {
	  afterEvaluate {
		println "JDK Javadoc link for ${p.name} is ${rootProject.jdkJavadoc}"
	  }
	}
  }

	asciidoctor {
	  sourceDir "$rootDir/docs/asciidoc/"
	  sources {
		include "index.asciidoc"
	  }
	  resources {
		from(sourceDir) {
		  include 'images/**'
		  include 'highlight/**/*'
		}
	  }
	  outputDir file("$buildDir/reactor-code/build/asciidoc")
	  backends = ['html5', 'pdf']
		logDocuments = true
		options = [
				doctype: 'book'
		]
		attributes 'allow-uri-read': '',
				imagesdir: "images/",
				stylesdir: "stylesheets/",
				stylesheet: 'reactor.css',
				'source-highlighter': 'highlightjs',
				'highlightjsdir': "./highlight",
				'highlightjs-theme': 'railscasts'

		doLast {
			file("$outputDir/pdf/index.pdf").renameTo ("$outputDir/pdf/reactor-core-reference-guide-${version}.pdf")
		}
	}

	dependencies {
	  asciidoctor 'org.jruby:jruby-complete:9.2.5.0'
	  asciidoctor 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
	}

	task docsZip(type: Zip, dependsOn: asciidoctor) {
	  baseName = 'reactor-core-docs'
	  from("$buildDir/reactor-code/build/asciidoc/pdf/reactor-core-reference-guide-${version}.pdf") { into ("docs/") }
	  from("$buildDir/reactor-code/build/asciidoc/html5/index.html") { into("docs/") }
	  from("$buildDir/reactor-code/build/asciidoc/html5/images") { into("docs/images/") }
	  from("$buildDir/reactor-code/build/asciidoc/html5/highlight") { into("docs/highlight/") }
	}
}

