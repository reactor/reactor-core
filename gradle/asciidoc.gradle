/*
 * Copyright (c) 2011-2021 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configure(rootProject) {
	apply plugin: 'org.asciidoctor.jvm.convert'
	apply plugin: 'org.asciidoctor.jvm.pdf'

	repositories {
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
	}

	// This configuration applies both to the asciidoctor & asciidoctorPdf tasks
	asciidoctorj {
		options = [doctype: 'book']
		attributes 'allow-uri-read': '',
				'attribute-missing': 'warn',
				'project-version': "${project.version}",
				'reactorReleaseTrain': "${bomVersion}"
	}

	asciidoctor {
		dependsOn "generateObservabilityDocs"
		inputs.dir(layout.buildDirectory.dir("generatedMetricsDocs/").get().asFile) // force the task to consider changes in this folder, making it not UP-TO-DATE
		sourceDir "docs/asciidoc/"
		sources {
			include "index.asciidoc"
		}
		baseDirFollowsSourceDir()
		resources {
			from(sourceDir) {
				include 'images/**'
				include 'highlight/**/*'
			}

		}
		outputDir layout.buildDirectory.dir("docs/asciidoc/html").get().asFile
		logDocuments = true
		attributes stylesdir: "stylesheets/",
				stylesheet: 'reactor.css',
				'source-highlighter': 'highlightjs',
				'highlightjsdir': "./highlight",
				'highlightjs-theme': 'railscasts',
				'reactorReleaseTrain': "$bomVersion"
	}

	asciidoctorPdf {
		onlyIf { isCiServer || !rootProject.version.toString().endsWith("-SNAPSHOT") || rootProject.hasProperty("forcePdf") }
		dependsOn "generateObservabilityDocs"
		sourceDir "docs/asciidoc/"
		sources {
			include "index.asciidoc"
		}
		baseDirFollowsSourceDir()
		outputDir layout.buildDirectory.dir("docs/asciidoc/pdf").get().asFile
		logDocuments = true
		attributes 'source-highlighter': 'rouge'
	}

	task asciidocs(dependsOn: [asciidoctor, asciidoctorPdf], group: "documentation") { }

	task docsZip(type: Zip, dependsOn: asciidocs) {
		archiveBaseName.set("reactor-core")
		archiveClassifier.set('docs')
		afterEvaluate() {
			//we configure the pdf copy late, when a potential customVersion has been applied to rootProject
			from(asciidoctorPdf) {
				into ("docs/")
				rename("index.pdf", "reactor-core-reference-guide-${rootProject.version}.pdf")
			}
		}
		from(asciidoctor) { into("docs/") }
	}

	configurations {
		adoc
	}

	dependencies {
		adoc libs.micrometer.docsGenerator
	}

	task generateObservabilityDocs(dependsOn: [
		"generateMeterListenerDocs",
		"generateTimedSchedulerDocs",
		"generateObservationDocs",
		"polishGeneratedMetricsDocs"]) {
	}

	task generateMeterListenerDocs(type: JavaExec) {
		mainClass.set("io.micrometer.docs.DocsGeneratorCommand")
		classpath configurations.adoc
		args project.rootDir.getAbsolutePath(),
				".*MicrometerMeterListenerDocumentation.*.java",
				project.rootProject.layout.buildDirectory.dir("generatedMetricsDocs/meterListener").get().asFile.absolutePath
	}

	task generateTimedSchedulerDocs(type: JavaExec) {
		mainClass.set("io.micrometer.docs.DocsGeneratorCommand")
		classpath configurations.adoc
		args project.rootDir.getAbsolutePath(), ".*TimedSchedulerMeterDocumentation.*.java",
				project.rootProject.layout.buildDirectory.dir("generatedMetricsDocs/timedScheduler").get().asFile.absolutePath
	}

	task generateObservationDocs(type: JavaExec) {
		mainClass.set("io.micrometer.docs.DocsGeneratorCommand")
		classpath configurations.adoc
		args project.rootDir.getAbsolutePath(),
				".*MicrometerObservationListenerDocumentation.*.java",
				project.rootProject.layout.buildDirectory.dir("generatedMetricsDocs/observation").get().asFile.absolutePath
	}

	task polishGeneratedMetricsDocs(type: Copy) {
		mustRunAfter "generateMeterListenerDocs"
		mustRunAfter "generateTimedSchedulerDocs"
		mustRunAfter "generateObservationDocs"
		from(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/generatedMetricsDocs/meterListener/") {
			include "_*.adoc"
			rename '_(.*).adoc', 'meterListener_$1.adoc'
		}
		from(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/generatedMetricsDocs/timedScheduler/") {
			include "_*.adoc"
			rename '_(.*).adoc', 'timedScheduler_$1.adoc'
		}
		from(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/generatedMetricsDocs/observation/") {
			include "_*.adoc"
			rename '_(.*).adoc', 'observation_$1.adoc'
		}
		into project.rootProject.layout.buildDirectory.get().asFile.toString() + "/documentedMetrics"
		filter { String line ->
			line.startsWith('[[observability-metrics]]') ||
				line.startsWith('=== Observability - Metrics') ||
				line.startsWith('Below you can find a list of all ') ||
				line.startsWith("Fully qualified name of the enclosing class ")
				? null : line
		}
		filter { String line -> line.startsWith("====") ? line.replaceFirst("====", "=") : line }
		doLast {
			//since these are the files that get explicitly included in asciidoc, smoke test they exist
			assert file(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/documentedMetrics/meterListener_metrics.adoc").exists()
			assert file(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/documentedMetrics/timedScheduler_metrics.adoc").exists()
			assert file(project.rootProject.layout.buildDirectory.get().asFile.toString() + "/documentedMetrics/observation_metrics.adoc").exists()
		}
	}

}

