/*
 * Copyright (c) 2011-2018 Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import japicmp.model.JApiCompatibility
import me.champeau.gradle.japicmp.JapicmpTask
import me.champeau.gradle.japicmp.report.Violation
import me.champeau.gradle.japicmp.report.stdrules.AbstractRecordingSeenMembers
import me.champeau.gradle.japicmp.report.stdrules.RecordSeenMembersSetup

buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.github.erizo.gradle:jcstress-gradle-plugin:0.8.4'
	}
}

apply plugin: 'idea' //needed to avoid IDEA seeing the jmh folder as source
apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'kotlin'
apply plugin: 'jcstress'

ext {
	bndOptions = [
		"Export-Package": [
			"!*internal*",
			"!reactor.blockhound*",
			"reactor.*"
		].join(","),
		"Import-Package": [
			"!javax.annotation",
			"!javax.annotation.meta",
			'org.slf4j;resolution:=optional;version="[1.5.4,2)"',
			"kotlin.*;resolution:=optional",
			"reactor.blockhound.*;resolution:=optional",
			"io.micrometer.*;resolution:=optional",
			"*"
		].join(","),
		"Bundle-Name" : "reactor-core",
		"Bundle-SymbolicName" : "io.projectreactor.reactor-core"
	]
}

testSets {
	blockHoundTest
	withMicrometerTest
}

configurations {
	compileOnly.extendsFrom jsr166backport
	testCompileOnly.extendsFrom jsr166backport
}

dependencies {
	// Reactive Streams
	api "org.reactivestreams:reactive-streams:${reactiveStreamsVersion}"
	testImplementation "org.reactivestreams:reactive-streams-tck:${reactiveStreamsVersion}"

	// JSR-305 annotations
	compileOnly "com.google.code.findbugs:jsr305:${findbugsVersion}"
	testCompileOnly "com.google.code.findbugs:jsr305:${findbugsVersion}"

	//Optional Logging Operator
	compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
	testCompileOnly "org.slf4j:slf4j-api:$slf4jVersion"

	//Optional Metrics
	compileOnly "io.micrometer:micrometer-core:$micrometerVersion"

	// Not putting kotlin-stdlib as implementation to not force it as a transitive lib
	compileOnly "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

	//Optional BlockHound support
	compileOnly "io.projectreactor.tools:blockhound:$blockhoundVersion"
	// Also make BlockHound visible in the CP of dedicated testset
	blockHoundTestImplementation "io.projectreactor.tools:blockhound:$blockhoundVersion"

	//Optional JDK 9 Converter
	jsr166backport "io.projectreactor:jsr166:$jsr166BackportVersion"

	testImplementation platform("org.junit:junit-bom:${jUnitPlatformVersion}")
	testImplementation "junit:junit:${jUnitVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testImplementation "org.junit.platform:junit-platform-launcher"
	testImplementation "org.junit.jupiter:junit-jupiter-params"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
	testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

	testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
	// Testing
	testImplementation(project(":reactor-test")) {
		exclude module: 'reactor-core'
	}

	testImplementation "org.assertj:assertj-core:$assertJVersion"
	testImplementation "org.testng:testng:$testNgVersion"
	testImplementation "org.mockito:mockito-core:$mockitoVersion"
	testImplementation "org.openjdk.jol:jol-core:$javaObjectLayoutVersion"
	testImplementation "pl.pragmatists:JUnitParams:$jUnitParamsVersion"
	testImplementation "org.awaitility:awaitility:$awaitilityVersion"
	testImplementation "com.pivovarit:throwing-function:$throwingFunctionVersion"
	testImplementation "com.tngtech.archunit:archunit:0.12.0"

	// withMicrometerTest is a test-set that validates what happens when micrometer *IS*
	// on the classpath. Needs sourceSets.test.output because tests there use helpers like AutoDisposingRule etc.
	withMicrometerTestImplementation "io.micrometer:micrometer-core:$micrometerVersion"
	withMicrometerTestImplementation sourceSets.test.output

}

task downloadBaseline(type: Download) {
	onlyIfNewer true
	compress true

	src "${repositories.jcenter().url}io/projectreactor/reactor-core/$compatibleVersion/reactor-core-${compatibleVersion}.jar"
	dest "${buildDir}/baselineLibs/reactor-core-${compatibleVersion}.jar"
}

class IncompatibleChangeWithExclusions extends AbstractRecordingSeenMembers implements Serializable {

	private final Map<String, String> acceptedViolations

	IncompatibleChangeWithExclusions(Map<String, String> params) {
		acceptedViolations = params
	}

	@Override
	public Violation maybeAddViolation(final JApiCompatibility memberRaw) {
		def member = Violation.describe(memberRaw)
		def memberViolationsStringList = memberRaw.compatibilityChanges.collect {it.toString() }
		def memberViolations = memberViolationsStringList.join(",")

		def memberAcceptedViolations = acceptedViolations[member]
		if (memberAcceptedViolations == memberViolations) {
			println "Excluded violation $memberAcceptedViolations for $member"
			return null;
		}
		if (memberAcceptedViolations) {
			println "Recognized exclusions for $member, but has more changes: $memberViolations"
		}
		if (!memberRaw.isBinaryCompatible()) {
			//let's see if there are generic binary exceptions:
			def acceptedBinaryViolations = acceptedViolations
				.getOrDefault("BINARY_COMPATIBLE", "")
				.tokenize(",")
				.toSet()
			if (acceptedBinaryViolations.containsAll(memberViolationsStringList)
				|| acceptedBinaryViolations.contains("*")) {
				return null;
			}
			return Violation.notBinaryCompatible(memberRaw);
		}
		else if (!memberRaw.isSourceCompatible()) {
			//let's see if there are generic source exceptions:
			def acceptedSourceViolations = acceptedViolations
				.getOrDefault("SOURCE_COMPATIBLE", "")
				.tokenize(",")
				.toSet()

			if (acceptedSourceViolations.containsAll(memberViolationsStringList)
				|| acceptedSourceViolations.contains("*")) {
				return null;
			}
			return Violation.error(memberRaw, "Is not source compatible", )
		}
		else {
			return null;
		}
	}
}

task japicmp(type: JapicmpTask) {
	if (project.gradle.startParameter.isOffline()) {
		println "Offline: skipping downloading of baseline and JAPICMP"
	  	enabled = false
	}
	else if ("$compatibleVersion" == "SKIP") {
		println "SKIP: Instructed to skip the baseline comparison"
	  	enabled = false
	}
	else {
		println "Will download and perform baseline comparison with ${compatibleVersion}"
	  	dependsOn(downloadBaseline)
	}

	oldClasspath = files("${buildDir}/baselineLibs/reactor-core-${compatibleVersion}.jar")
	newClasspath = files(jar.archiveFile)
	onlyBinaryIncompatibleModified = false
	failOnModification = false
	failOnSourceIncompatibility = false
	txtOutputFile = file("${project.buildDir}/reports/japi.txt")
	ignoreMissingClasses = true
	includeSynthetic = true

	//TODO after a release, bump the gradle.properties baseline
	//TODO after a release, remove the reactor-core exclusions below if any
	classExcludes = [
			"reactor.core.publisher.TopicProcessor",
			"reactor.core.publisher.WorkQueueProcessor",
			'reactor.core.publisher.EventLoopProcessor$Slot',
			'reactor.util.concurrent.WaitStrategy',
	]
	methodExcludes = [
			'reactor.core.publisher.Flux#onLastAssembly(reactor.core.publisher.Flux)',
			'reactor.core.publisher.Flux#compose(java.util.function.Function)',
			'reactor.core.publisher.Flux#retry(java.util.function.Predicate)',
			'reactor.core.publisher.Flux#retry(long, java.util.function.Predicate)',
			'reactor.core.publisher.Flux#retryBackoff(long, java.time.Duration)',
			'reactor.core.publisher.Flux#retryBackoff(long, java.time.Duration, java.time.Duration)',
			'reactor.core.publisher.Flux#retryBackoff(long, java.time.Duration, java.time.Duration, reactor.core.scheduler.Scheduler)',
			'reactor.core.publisher.Flux#retryBackoff(long, java.time.Duration, java.time.Duration, double)',
			'reactor.core.publisher.Flux#retryBackoff(long, java.time.Duration, java.time.Duration, double, reactor.core.scheduler.Scheduler)',
			'reactor.core.publisher.Flux#retryWhen(java.util.function.Function)',
			'reactor.core.publisher.Flux#usingWhen(org.reactivestreams.Publisher, java.util.function.Function, java.util.function.Function, java.util.function.Function)',
			'reactor.core.publisher.Flux#usingWhen(org.reactivestreams.Publisher, java.util.function.Function, java.util.function.Function, java.util.function.Function, java.util.function.Function)',

			'reactor.core.publisher.Mono#onLastAssembly(reactor.core.publisher.Mono)',
			'reactor.core.publisher.Mono#compose(java.util.function.Function)',
			'reactor.core.publisher.Mono#retry(java.util.function.Predicate)',
			'reactor.core.publisher.Mono#retry(long, java.util.function.Predicate)',
			'reactor.core.publisher.Mono#retryBackoff(long, java.time.Duration)',
			'reactor.core.publisher.Mono#retryBackoff(long, java.time.Duration, java.time.Duration)',
			'reactor.core.publisher.Mono#retryBackoff(long, java.time.Duration, java.time.Duration, reactor.core.scheduler.Scheduler)',
			'reactor.core.publisher.Mono#retryBackoff(long, java.time.Duration, java.time.Duration, double)',
			'reactor.core.publisher.Mono#retryBackoff(long, java.time.Duration, java.time.Duration, double, reactor.core.scheduler.Scheduler)',
			'reactor.core.publisher.Mono#retryWhen(java.util.function.Function)',
			'reactor.core.publisher.Mono#usingWhen(org.reactivestreams.Publisher, java.util.function.Function, java.util.function.Function, java.util.function.Function)',
			'reactor.core.publisher.Mono#usingWhen(org.reactivestreams.Publisher, java.util.function.Function, java.util.function.Function, java.util.function.Function, java.util.function.Function)',

			'reactor.core.publisher.ParallelFlux#composeGroup(java.util.function.Function)',
	]

	Map<String, String> specificChangeExclusions = [
		// class Excludes
		"Class reactor.core.publisher.MonoProcessor" : "CLASS_NOW_ABSTRACT", //MonoProcessor was final so should be fine to be now abstract

		// method Excludes

		// these methods are now abstract, should be source compatible
		"Method reactor.core.publisher.MonoProcessor.onSubscribe(org.reactivestreams.Subscription)" : "METHOD_REMOVED",
		"Method reactor.core.publisher.MonoProcessor.onNext(java.lang.Object)" : "METHOD_REMOVED",
		"Method reactor.core.publisher.MonoProcessor.onError(java.lang.Throwable)" : "METHOD_REMOVED",
		"Method reactor.core.publisher.MonoProcessor.onComplete()" : "METHOD_REMOVED",

		// generic excludes
		"SOURCE_COMPATIBLE" : "METHOD_ADDED_TO_INTERFACE" //needed for the rule to ignore new default methods
	]

	richReport {
		addDefaultRules = false
		addSetupRule(RecordSeenMembersSetup)
		addRule(IncompatibleChangeWithExclusions, specificChangeExclusions)
	}
}

gradle.taskGraph.afterTask { task, state ->
	if (task instanceof JapicmpTask && state.failure && ((JapicmpTask) task).richReport == null) {
		//FIXME print the rich report somehow on console ?
		print file("${project.buildDir}/reports/japi.txt").getText()
	}
}

//complements the javadoc.gradle common configuration
javadoc {
	options.overview = "$rootDir/docs/api/overview.html"
	excludes = [
			// Must be public due to the ServiceLoader's requirements
			"reactor/core/scheduler/ReactorBlockHoundIntegration.java"
	]
	doLast {
		// work around https://github.com/gradle/gradle/issues/4046
		copy {
			from('src/main/java')
			into "$project.buildDir/docs/javadoc/"
			include "**/doc-files/**/*"
		}
	}
}

task loops(type: Test, group: 'verification') {
	include '**/*Loop.*'
	doFirst {
		println "Additional tests from `loops` ($includes)"
	}
}

task testNG(type: Test, group: 'verification') {
	useTestNG()
	include '**/*Verification.*'
	doFirst {
		println "Additional tests from `testNG` ($includes)"
	}
}

blockHoundTest {
	// Creates a JVM per test because the agent can be installed only once
	forkEvery = 1
	maxParallelForks = 1
}

tasks.withType(Test).all {
  useJUnitPlatform()
}

//inherit basic test task + common configuration in root
//always depend on testNoMicrometer, skip testNG on Travis, skip loops when not releasing
//note that this way the tasks can be run individually
check {
	dependsOn withMicrometerTest
	if (!detectedCiServers.contains("TRAVIS")) {
		dependsOn testNG
	}
	if (!version.endsWith('-SNAPSHOT') && !version.contains('-SNAPSHOT-')) { //neither classic nor customized snapshots
		dependsOn loops
	}
}

//TODO all java9 / stubs / java-specific stuff should go in a convention plugin ?
if (!JavaVersion.current().isJava9Compatible()) {
	test {
		jvmArgs = ["-Xbootclasspath/p:" + configurations.jsr166backport.asPath]
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'reactor-core',
			'Implementation-Version': project.version,
			'Automatic-Module-Name': 'reactor.core'
	}
	bnd(bndOptions)
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport
check.dependsOn japicmp

jcstress {
	mode = 'quick'
}
tasks.jcstressJar.classifier("jcstress") // make sure jcstress jar doesn't override our own
tasks.check.dependsOn(tasks.jcstress)

if (JavaVersion.current().java9Compatible) {
	sourceSets {
		java8stubs.java.srcDirs = ['src/main/java8stubs']
	}

	tasks.withType(JavaCompile).all {
		sourceCompatibility = targetCompatibility = 8
	}

	tasks.withType(Javadoc).all {
		excludes = ["reactor/core/publisher/Traces.java"]
	}

	dependencies {
		compileOnly sourceSets.java8stubs.output
	}
}
else {
	sourceSets {
		java9stubs.java.srcDirs = ['src/main/java9stubs']
	}

	dependencies {
		compileOnly sourceSets.java9stubs.output
	}
}

//add docs.zip to the publication
publishing.publications.mavenJava.artifact(rootProject.tasks.docsZip)